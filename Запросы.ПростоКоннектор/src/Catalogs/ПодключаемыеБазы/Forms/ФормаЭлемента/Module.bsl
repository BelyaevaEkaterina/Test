#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НачальноеЗаполнениеТаблиц();
	Иначе
		УстановитьОтборыТаблиц();
	КонецЕсли;
	
	ЗаполнениеСпискаВыбораСервисаИнтеграции();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Не ПустаяСтрока(Объект.СервисИнтеграции) Тогда
	    ПодключитьОбработчикОжидания("ВыполнитьПроверкуАктивностиСервера", 0.1, Истина);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Направление) Тогда
		НастроитьОтображениеСтраниц(Объект.Направление);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	ВыполнитьПроверкуАктивностиСервера();
КонецПроцедуры

&НаКлиенте
Процедура ТолькоВыбранныеСправочники(Команда)
	Использование = Элементы.НастройкиВыгрузкиСправочникиТолькоВыбранныеСправочники.Пометка;
	Если Не Использование Тогда
		Элементы.НастройкиВыгрузкиСправочникиТолькоВыбранныеСправочники.Пометка = Истина;
		ВключитьУОНаСервере(Истина, 0);
	Иначе
		Элементы.НастройкиВыгрузкиСправочникиТолькоВыбранныеСправочники.Пометка = Ложь;
		ОчиститьУО();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоВыбранныеДокументы(Команда)
	Использование = Элементы.НастройкиВыгрузкиДокументыТолькоВыбранныеДокументы.Пометка;
	Если Не Использование Тогда
		Элементы.НастройкиВыгрузкиДокументыТолькоВыбранныеДокументы.Пометка = Истина;
		ВключитьУОНаСервере(Истина, 1);
	Иначе
		Элементы.НастройкиВыгрузкиДокументыТолькоВыбранныеДокументы.Пометка = Ложь;
		ОчиститьУО();
	КонецЕсли;	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементов 

&НаКлиенте
Процедура СервисИнтеграцииПриИзменении(Элемент)
	СервисПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КаналПриИзменении(Элемент)
	Если Не ПустаяСтрока(Объект.СервисИнтеграции) Тогда
	    ПодключитьОбработчикОжидания("ВыполнитьПроверкуАктивностиСервера", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

 &НаКлиенте
Процедура НаправлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	НастроитьОтображениеСтраниц(ВыбранноеЗначение);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НастроитьОтображениеСтраниц(Значение)
     // Список соответствий между направлением и элементами
    СписокОтображения = Новый Соответствие;
    СписокОтображения.Вставить(ПредопределенноеЗначение("Перечисление.Направление.Выгрузка"), "ВыгрузкаДанных");
    СписокОтображения.Вставить(ПредопределенноеЗначение("Перечисление.Направление.Загрузка"), "ЗагрузкаДанных");

    // Скрываем все страницы
    Для каждого Страница Из СписокОтображения Цикл
        Элементы.ГруппаСтраницы.ПодчиненныеЭлементы[Страница.Значение].Видимость = Ложь;
    КонецЦикла;

    // Отображаем нужную страницу
    Если СписокОтображения[Значение] <> Неопределено Тогда
        Элементы.ГруппаСтраницы.ПодчиненныеЭлементы[СписокОтображения[Значение]].Видимость = Истина;
    КонецЕсли;
КонецПроцедуры

#Область РаботаСТабличнойЧастьюНастройкиВыгрузки

&НаСервере
Процедура НачальноеЗаполнениеТаблиц() 
	МассивИсключений = Новый Массив;
	МассивИсключений.Добавить("Подключаемые базы");
	МассивИсключений.Добавить("Правила выгрузки даннах");
	МассивИсключений.Добавить("Правила загрузки данных");
	
	// Добавляем справочники
	Для Каждого Справочник Из Метаданные.Справочники Цикл
		СправочникИсключение = МассивИсключений.Найти(Справочник.Представление());
		Если СправочникИсключение <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Объект.НастройкиВыгрузкиДанных.Добавить();
		НоваяСтрока.Метаданные = Справочник.Представление();
		НоваяСтрока.ТипМетаданных = "Справочник";
		
		НоваяСтрокаЗагрузка = Объект.НастройкиЗагрузки.Добавить();
		НоваяСтрокаЗагрузка.ТипМетаданных = Справочник.Представление();
	КонецЦикла;
	
	// Добавляем документы
	Для Каждого Документ Из Метаданные.Документы Цикл
		НоваяСтрока = Объект.НастройкиВыгрузкиДанных.Добавить();
		НоваяСтрока.Метаданные = Документ.Представление();
		НоваяСтрока.ТипМетаданных = "Документ";
		
		НоваяСтрокаЗагрузка = Объект.НастройкиЗагрузки.Добавить();
		НоваяСтрокаЗагрузка.ТипМетаданных = Документ.Представление();
	КонецЦикла;
	
	// Установим отборы
	УстановитьОтборыТаблиц();
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборыТаблиц()
	Элементы.НастройкиВыгрузкиСправочники.ОтборСтрок = Новый ФиксированнаяСтруктура("ТипМетаданных", "Справочник");
	Элементы.НастройкиВыгрузкиДокументы.ОтборСтрок = Новый ФиксированнаяСтруктура("ТипМетаданных", "Документ");
КонецПроцедуры

#КонецОбласти

#Область СервисИнтеграцииКаналы 

&НаСервере
Процедура ЗаполнениеСпискаВыбораСервисаИнтеграции()
	Если Метаданные.СервисыИнтеграции.Количество() > 0 Тогда
		Для Каждого Сервис Из Метаданные.СервисыИнтеграции Цикл
			Элементы.СервисИнтеграции.СписокВыбора.Добавить(Сервис.Имя, Сервис.Имя);
		КонецЦикла;
		
		Если Элементы.СервисИнтеграции.СписокВыбора.Количество() > 0 Тогда 
			СервисПриИзмененииНаСервере();
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКаналыНаСервере(СервисИнтеграции)
	МассивКаналов = Новый Массив;  

	Для каждого Канал Из Метаданные.СервисыИнтеграции[СервисИнтеграции].КаналыСервисаИнтеграции Цикл
	    МассивКаналов.Добавить(Канал.Имя);
	КонецЦикла;
	
	Возврат МассивКаналов;
КонецФункции	

&НаКлиенте
Процедура ВыполнитьПроверкуАктивностиСервера()
	АктиавностьСервиса = ПроверитьПодключениеНаСервере(Объект.СервисИнтеграции);
	
	Если АктиавностьСервиса Тогда
		Элементы.ДекорацияАктивность.Заголовок = "Активен";
		Элементы.ДекорацияАктивность.ЦветТекста = WebЦвета.Зеленый;
	Иначе
		Элементы.ДекорацияАктивность.Заголовок = "Не активен";
		Элементы.ДекорацияАктивность.ЦветТекста = WebЦвета.Красный;
	КонецЕсли;	
КонецПроцедуры	

&НаСервереБезКонтекста
Функция ПроверитьПодключениеНаСервере(СервисИнтеграции)
	Возврат СервисыИнтеграции[СервисИнтеграции].ПолучитьАктивность();
КонецФункции	

&НаСервере
Процедура СервисПриИзмененииНаСервере()
	Если Элементы.Канал.СписокВыбора.Количество() > 0 Тогда
		Элементы.Канал.СписокВыбора.Очистить();
	КонецЕсли;
	
	Сервис = Метаданные.СервисыИнтеграции.Найти(Объект.СервисИнтеграции);
	
	Если Сервис <> Неопределено Тогда
		Если Объект.Направление = Перечисления.Направление.Выгрузка Тогда
			КаналыСервисаИнтеграцииОтправка = Метаданные.СвойстваОбъектов.НаправлениеСообщенияКаналаСервисаИнтеграции.Отправка;
		Иначе
			КаналыСервисаИнтеграцииОтправка = Метаданные.СвойстваОбъектов.НаправлениеСообщенияКаналаСервисаИнтеграции.Получение;
		КонецЕсли;
		
		Для Каждого КаналСервиса Из Сервис.КаналыСервисаИнтеграции Цикл 
			Если КаналСервиса.НаправлениеСообщения = КаналыСервисаИнтеграцииОтправка Тогда 
				Элементы.Канал.СписокВыбора.Добавить(КаналСервиса.Имя, КаналСервиса.Имя);				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ВключитьУОНаСервере(Использование, Настройка) 
	УсловноеОформление.Элементы.Очистить();
	
	УО = УсловноеОформление.Элементы.Добавить();
	ОформлениеВидимость = УО.Оформление.Элементы.Найти("Видимость");
	ОформлениеВидимость.Значение = Ложь;
	ОформлениеВидимость.Использование = Использование;
	            
	НастройкаОтбора = УО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НастройкаОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиВыгрузкиДанных.Выбрать");
	НастройкаОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	НастройкаОтбора.ПравоеЗначение = Ложь;
	
	МассивИменПолей = Новый Массив;
	Если Настройка = 0 Тогда
		МассивИменПолей.Добавить("НастройкиВыгрузкиСправочникиМетаданные");
		МассивИменПолей.Добавить("НастройкиВыгрузкиДанныхТипМетаданных");
		МассивИменПолей.Добавить("НастройкиВыгрузкиДанныхПравилоВыгрузки");
		МассивИменПолей.Добавить("НастройкиВыгрузкиДанныхВыбрать");
		
	Иначе
		МассивИменПолей.Добавить("НастройкиВыгрузкиДанныхМетаданные");
		МассивИменПолей.Добавить("НастройкиВыгрузкиДанныхТипМетаданных1");
		МассивИменПолей.Добавить("НастройкиВыгрузкиДанныхПравилоВыгрузки1");
		МассивИменПолей.Добавить("НастройкиВыгрузкиДанныхВыбрать1");
	КонецЕсли;
	
	Для каждого Поле Из МассивИменПолей Цикл
		ОформляемыеПоля = УО.Поля.Элементы.Добавить(); 
		ОформляемыеПоля.Поле = Новый ПолеКомпоновкиДанных(Поле);
		ОформляемыеПоля.Использование = Использование;
	КонецЦикла; 
КонецПроцедуры	

&НаСервере
Процедура ОчиститьУО() 
	УсловноеОформление.Элементы.Очистить();
КонецПроцедуры	

#КонецОбласти 



