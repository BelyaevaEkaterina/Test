 
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВстроенныйЯзык = Объект.ИсполняемыйКод;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.УсловияВыгрузкиИсполныемыйКод = "//Источник 
		|Результат = Истина;";	    
		
		Объект.ИсполняемыйКод =
		"//Источник
		|СтруктураОбъекта = Коннектор_БиблиотекаФункций.ПолучитьСтруктуруОбъекта(Источник);
		|ТелоСообщения = Коннектор_БиблиотекаФункций.ТелоСообщенияКакJSON(СтруктураОбъекта);";
	КонецЕсли;
	
	ВерсияБСП = Коннектор_БиблиотекаФункцийПовтИсп.ВерсияБСП();
	МассивДоступныхВерсий = Новый Соответствие;
	КоллекцияРасширений = Новый Соответствие;
	
	Если ВерсияБСП <> "" Тогда
		ОбменДаннымиПереопределяемый.ПриПолученииДоступныхВерсийФормата(МассивДоступныхВерсий);
		ОбменДаннымиПереопределяемый.ПриПолученииДоступныхРасширенийФормата(КоллекцияРасширений);
	КонецЕсли;
	
	Для Каждого Расширение Из КоллекцияРасширений Цикл
		СтрокаРасширения = РасширенияФормата.Добавить();
		СтрокаРасширения.ПространствоИмен = Расширение.Ключ;
		СтрокаРасширения.БазоваяВерсия    = Расширение.Значение;
	КонецЦикла;
	
	ЗаполнитьВерсиюФорматаПоУмолчанию(МассивДоступныхВерсий);
	
	УстановитьВидимостьДоступность();
	УстановитьУсловияСКДНаСервере(Ложь);
	Коннектор_БиблиотекаФункций.УстановитьОтборСпискаСвойствКонструктора(СписокСвойствКонструктора, "Владелец", Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Настройки = КомпановщикНастроекСКД.ПолучитьНастройки();
	ТекущийОбъект.НастройкиСКД = Новый ХранилищеЗначения(Настройки);
	ТекущийОбъект.УсловиеВыгрузкиСКДУстановлено = КомпановщикНастроекСКД.Настройки.Отбор.Элементы.Количество();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантПроверкиУсловияВыгрузкиПриИзменении(Элемент)

	Объект.Источник = "";
	
	УстановитьВидимостьДоступность(); 
	УстановитьУсловияСКДНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура ИсточникПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность(); 
	УстановитьУсловияСКДНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантВыгрузкиПриИзменении(Элемент)
	
	Объект.Источник = "";
	
	УстановитьВидимостьДоступность(); 
	УстановитьУсловияСКДНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВерсияФорматаПриИзменении(Элемент)
	
	ОбновитьПравилаЗагрузкиНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовПолейНастроек

&НаКлиенте
Процедура СписокСвойствКонструктораПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Модифицированность Тогда
		ПоказатьПредупреждение(, "Перед добавлением настроек, необходимо сохранить правило.");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	Элементы.УсловияВыгрузкиИсполныемыйКод.Видимость =
		Объект.ВариантПроверкиУсловияВыгрузки = Перечисления.ВариантыПроверкиУсловийВыгрузки.ИсполняемыйКод;
		
	Элементы.КомпановщикНастроекСКДНастройкиОтбор.Видимость =
		Объект.ВариантПроверкиУсловияВыгрузки = Перечисления.ВариантыПроверкиУсловийВыгрузки.СКД;
		
	Элементы.ИсполняемыйКод.Видимость =
		Объект.ВариантВыгрузки = Перечисления.ВариантыВыгрузки.ИсполняемыйКод;
		
	Элементы.ВерсияФормата.Видимость =
		Объект.ВариантВыгрузки = Перечисления.ВариантыВыгрузки.EnterpriseData;
		
	Элементы.РасширениеФормата.Видимость =
		Объект.ВариантВыгрузки = Перечисления.ВариантыВыгрузки.EnterpriseData;
		
	Элементы.ПравилоКонвертации.Видимость =
		Объект.ВариантВыгрузки = Перечисления.ВариантыВыгрузки.КД2;
		
	Элементы.КомпановщикНастроекСКДНастройкиВыбор.Видимость =
		Объект.ВариантВыгрузки = Перечисления.ВариантыВыгрузки.СКД;
		
	Элементы.ГруппаНастройкаКонструктора.Видимость = 
		Объект.ВариантВыгрузки = Перечисления.ВариантыВыгрузки.Конструктор;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловияСКДНаСервере(Знач УстановитьПоУмолчанию = Истина)
	
	Если Не ЗначениеЗаполнено(Объект.Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = Коннектор_БиблиотекаФункций.ТекстЗапросаПоИмениМетаданных(Объект.Источник);
	
	ОбъектРеквизит = РеквизитФормыВЗначение("Объект");
	НастройкиСКД = ОбъектРеквизит.НастройкиСКД.Получить();
	
	НовыйСКД = ОбъектРеквизит.ПолучитьМакет("Макет"); 
	Если ТекстЗапроса <> "" Тогда 
		НовыйСКД.НаборыДанных.НаборДанных1.Запрос = ТекстЗапроса;
	КонецЕсли;
	
	АдресСхемы = ПоместитьВоВременноеХранилище(НовыйСКД, Новый УникальныйИдентификатор);
	КомпановщикНастроекСКД.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
	
	Если УстановитьПоУмолчанию Или НастройкиСКД = Неопределено Тогда
		КомпановщикНастроекСКД.ЗагрузитьНастройки(НовыйСКД.НастройкиПоУмолчанию);
	Иначе
		КомпановщикНастроекСКД.ЗагрузитьНастройки(НастройкиСКД);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВерсиюФорматаПоУмолчанию(МассивДоступныхВерсий)
	
	Если МассивДоступныхВерсий.Количество() > 0 Тогда
		
		Элементы.ВерсияФормата.СписокВыбора.Очистить();
		
		ПредставлениеСпискаВерсий = "";
		Для Каждого ЭлементМассива Из МассивДоступныхВерсий Цикл
			ПредставлениеСпискаВерсий = ПредставлениеСпискаВерсий + "; " + ЭлементМассива.Ключ;
			Элементы.ВерсияФормата.СписокВыбора.Добавить(ЭлементМассива.Ключ);
		КонецЦикла;
		
		ПредставлениеСпискаВерсий = Сред(ПредставлениеСпискаВерсий, 3);
		СтрокаПоддерживаемыеВерсииФормата = СтрШаблон(НСтр("ru = 'Поддерживаемые версии формата: %1'"), ПредставлениеСпискаВерсий);
		Объект.ВерсияФормата = Элементы.ВерсияФормата.СписокВыбора[МассивДоступныхВерсий.Количество()-1].Значение;
	
	КонецЕсли;
	
КонецПроцедуры  

&НаСервере
Процедура ОбновитьПравилаЗагрузкиНаСервере()
	
	Объект.РасширениеФормата = "";
	Элементы.РасширениеФормата.СписокВыбора.Очистить();
	Элементы.РасширениеФормата.СписокВыбора.Добавить("", "");
	Для Каждого Расширение Из РасширенияФормата.НайтиСтроки(Новый Структура("БазоваяВерсия", Объект.ВерсияФормата)) Цикл
		Элементы.РасширениеФормата.СписокВыбора.Добавить(Расширение.ПространствоИмен);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура; 
	ПараметрыФормы.Вставить("ВерсияФормата", Объект.ВерсияФормата);
	ПараметрыФормы.Вставить("РасширениеФормата", Объект.РасширениеФормата);
	ПараметрыФормы.Вставить("ВариантВыгрузки", Объект.ВариантВыгрузки);
	
	ОткрытьФорму("Обработка.Коннектор_ВыборИсточника.Форма.ФормаПодбораИсточника", ПараметрыФормы, Элементы.Источник, , , , ,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

