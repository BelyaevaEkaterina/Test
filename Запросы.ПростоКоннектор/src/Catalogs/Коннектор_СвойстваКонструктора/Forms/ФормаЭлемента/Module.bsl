#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
		
	Если НЕ ЗаписьПодчиненногоПодтверждена И СвойствоНастроеноКакПоле(Объект.Родитель) Тогда
		
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Родительское свойство является полем. После записи текущего элемента родительские настройки заполнения будут очищены. Продолжить?'");
		ООП = Новый ОписаниеОповещения("ЗаписатьПослеПодтверждения", ЭтаФорма);
		ПоказатьВопрос(ООП, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьПослеПодтверждения(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		ЗаписьПодчиненногоПодтверждена = Истина;
		Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВерсияБСП = Коннектор_БиблиотекаФункцийПовтИсп.ВерсияБСП();
	
	Если Не Объект.Владелец.Пустая() Тогда
		
		Если ВерсияБСП = "" Тогда
			Источник = Объект.Владелец.Источник;
		Иначе
			Источник = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "Источник");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПустаяСтрока(Источник) Тогда
		
		Источник = "Не указан источник данных в правиле обмена!";
		Элементы.Источник.ЦветТекста = WebЦвета.Красный;
		
	Иначе
		
		ИмяИсточника = Коннектор_БиблиотекаФункций.ПолучитьИмяМетаданныхИзСтроки(Источник);
		//МассивРеквизитовИсточника = МассивРеквизитовИсточника();
		//Элементы.РеквизитИС.СписокВыбора.ЗагрузитьЗначения(МассивРеквизитовИсточника);
		
	КонецЕсли;
	
	Если Не Объект.Ссылка.Пустая() Тогда
		ЭтоРодитель = ЭтоРодитель();
		Коннектор_БиблиотекаФункций.УстановитьОтборСпискаСвойствКонструктора(СписокДочернихЭлементов, "Родитель", Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ВариантПравилЗаполненияСвойстваПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитИСПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.Наименование) Тогда
		Объект.Наименование = Объект.РеквизитИС;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыражениеНаВстроенномЯзыкеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеРедактированияВыражения", ЭтотОбъект);
	ПараметрыОткрытияФормы = Новый Структура("ТекстВыражения, ЭтоМассив", Объект.ВыражениеНаВстроенномЯзыке, Объект.ЭтоМассив);
	ОткрытьФорму("Справочник.Коннектор_СвойстваКонструктора.Форма.ФормаРедактированияВыражения",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,
		"ФормаРедактированияВыраженияКонструктора",
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтоМассивПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РедактироватьНедоступно(Команда)
	РедактироватьНедоступныеЭлементы = Не РедактироватьНедоступныеЭлементы;
	ИзменитьРедактированиеНедоступныхЭлементы();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступность()
	
	ВариантРеквизитОбъекта = ПредопределенноеЗначение("Перечисление.Коннектор_ВариантыПравилЗаполненияСвойствКонструктора.РеквизитОбъекта");
	ВариантВыражение = ПредопределенноеЗначение("Перечисление.Коннектор_ВариантыПравилЗаполненияСвойствКонструктора.ВыражениеНаВстроенномЯзыке");
	
	Если Объект.ЭтоМассив Тогда
		Объект.ВариантПравилЗаполненияСвойства = ВариантВыражение;
		Элементы.ВариантПравилЗаполненияСвойства.Доступность = Ложь;
	Иначе
		Элементы.ВариантПравилЗаполненияСвойства.Доступность = Истина;
	КонецЕсли;
	
	Если Объект.ВариантПравилЗаполненияСвойства = ВариантРеквизитОбъекта Тогда
		Элементы.ВыражениеНаВстроенномЯзыке.Доступность = Ложь;
		Элементы.РеквизитИС.Доступность = Истина;
	ИначеЕсли Объект.ВариантПравилЗаполненияСвойства = ВариантВыражение Тогда
		Элементы.ВыражениеНаВстроенномЯзыке.Доступность = Истина;
		Элементы.РеквизитИС.Доступность = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаВариантыПравил.Видимость = Не ЭтоРодитель;
	Элементы.ГруппаСписокДочерних.Видимость = ЭтоРодитель;
	Элементы.ЭтоМассив.Видимость = Не ЭтоРодитель;
	
	ИзменитьРедактированиеНедоступныхЭлементы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРедактированиеНедоступныхЭлементы()
	
	Если РедактироватьНедоступныеЭлементы Тогда
		Элементы.Владелец.Видимость = Истина;
		Элементы.Код.Видимость = Истина;
	Иначе
		Элементы.Код.Видимость = Ложь;
		Если Не Объект.Владелец.Пустая() Тогда
			Элементы.Владелец.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

//&НаСервере
//Функция МассивРеквизитовИсточника()
//	
//	МассивРеквизитов = Новый Массив;
//	
//	ТипМетаданныхСтрокой = Лев(Источник, СтрНайти(Источник, ".") - 1);
//	
//	Если ТипМетаданныхСтрокой = "Справочник" Тогда
//		ТипМетаданныхСтрокой = "Справочники";
//	ИначеЕсли ТипМетаданныхСтрокой = "Документ" Тогда
//		ТипМетаданныхСтрокой = "Документы";
//	ИначеЕсли ТипМетаданныхСтрокой = "ПланВидовХарактеристик" Тогда
//		ТипМетаданныхСтрокой = "ПланыВидовХарактеристик";
//	КонецЕсли;
//	
//	ОбъектМетаданных = Метаданные[ТипМетаданныхСтрокой][ИмяИсточника];
//	Для Каждого РеквизитОбъекта Из ОбъектМетаданных.СтандартныеРеквизиты Цикл
//		МассивРеквизитов.Добавить(РеквизитОбъекта.Имя);
//	КонецЦикла;
//	
//	Для Каждого РеквизитОбъекта Из ОбъектМетаданных.Реквизиты Цикл
//		МассивРеквизитов.Добавить(РеквизитОбъекта.Имя);
//	КонецЦикла;
//	
//	Возврат МассивРеквизитов;
//	
//КонецФункции

&НаКлиенте
Процедура ПослеРедактированияВыражения(ЗначениеВыражения, ДопПараметры) Экспорт
	
	Если ЗначениеВыражения <> Неопределено Тогда
		Объект.ВыражениеНаВстроенномЯзыке = ЗначениеВыражения;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭтоРодитель()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Коннектор_СвойстваКонструктора.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Коннектор_СвойстваКонструктора КАК Коннектор_СвойстваКонструктора
		|ГДЕ
		|	Коннектор_СвойстваКонструктора.Родитель = &ЭтотОбъект
		|	И НЕ Коннектор_СвойстваКонструктора.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ЭтотОбъект", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

&НаСервереБезКонтекста
Функция СвойствоНастроеноКакПоле(Свойство)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Свойство);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Коннектор_СвойстваКонструктора.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Коннектор_СвойстваКонструктора КАК Коннектор_СвойстваКонструктора
	|ГДЕ
	|	Коннектор_СвойстваКонструктора.Ссылка = &Ссылка
	|	И (Коннектор_СвойстваКонструктора.ЭтоМассив
	|			ИЛИ Коннектор_СвойстваКонструктора.РеквизитИС <> """"
	|			ИЛИ (ВЫРАЗИТЬ(Коннектор_СвойстваКонструктора.ВыражениеНаВстроенномЯзыке КАК СТРОКА(50))) <> """")";
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
		
КонецФункции

&НаКлиенте
Процедура РеквизитИСНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбораРеквизита = Новый Структура;
	ПараметрыВыбораРеквизита.Вставить("ТипОбъекта", Источник);
	ПараметрыВыбораРеквизита.Вставить("ИмяРеквизитаОбъектаИС", "");
	ПараметрыВыбораРеквизита.Вставить("ПредставлениеРеквизитаОбъектаДО", "");
	ПараметрыВыбораРеквизита.Вставить("Таблица", Неопределено);
	ПараметрыВыбораРеквизита.Вставить("ЭтоТаблица", Ложь);
	
	ООП = Новый ОписаниеОповещения("ПослеВыбораРеквизита", ЭтаФорма, Новый Структура);
	ОткрытьФорму("Справочник.Коннектор_СвойстваКонструктора.Форма.ВыборРеквизитаПотребителя", ПараметрыВыбораРеквизита, ЭтаФорма,,,,ООП,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораРеквизита(Результат, ДопПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Объект.РеквизитИС = Результат.Имя;
	
КонецПроцедуры

#КонецОбласти












































