		 
#Область ОбработчикиСобытийФормы		 
		 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Коннектор_АсинхронныйОбмен = Константы.Коннектор_АсинхронныйОбмен.Получить();
	Коннектор_ЧислоПотоков = Константы.Коннектор_ЧислоПотоков.Получить();
	Коннектор_КаналОтправка = Константы.Коннектор_КаналОтправка.Получить();
	    
	АктивностьСервиса = СервисыИнтеграции.Коннектор_1СШина.ПолучитьАктивность(); 
	НастройкиСервиса = СервисыИнтеграции.Коннектор_1СШина.ПолучитьНастройки();   
	
	ИмяПользователяВнешнегоСервисаИнтеграции = НастройкиСервиса.ИмяПользователяВнешнегоСервисаИнтеграции;    
	ПарольПользователяВнешнегоСервисаИнтеграции = НастройкиСервиса.ПарольПользователяВнешнегоСервисаИнтеграции;  
	АдресВнешнегоСервисаИнтеграции = НастройкиСервиса.АдресВнешнегоСервисаИнтеграции;
	
	Для Каждого Эл Из Метаданные.СервисыИнтеграции.Коннектор_1СШина.КаналыСервисаИнтеграции Цикл     
		Если Строка(Эл.НаправлениеСообщения) = "Отправка" Тогда
			Элементы.Коннектор_КаналОтправка.СписокВыбора.Добавить(Эл.Имя, Эл.Имя);
		КонецЕсли;
	КонецЦикла;      
		
КонецПроцедуры 

#КонецОбласти     

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьНастройки(Команда)  
	
	ЗаписатьНастройкиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПароль(Команда)

	Элементы.ПарольПользователяВнешнегоСервисаИнтеграции.РежимПароля = Не 
	Элементы.ПарольПользователяВнешнегоСервисаИнтеграции.РежимПароля;
	
	Если Элементы.ПарольПользователяВнешнегоСервисаИнтеграции.РежимПароля Тогда 
		Элементы.ПоказатьПароль.Картинка = КартинкаПоказатьПароль; 
	Иначе	
		Элементы.ПоказатьПароль.Картинка = КартинкаСкрытьПароль;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти   

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьНастройкиНаСервере()
	
	УстановитьПривилегированныйРежим(Истина); 
	
	Константы.Коннектор_АсинхронныйОбмен.Установить(Коннектор_АсинхронныйОбмен);
	Константы.Коннектор_ЧислоПотоков.Установить(Коннектор_ЧислоПотоков);
	Константы.Коннектор_КаналОтправка.Установить(Коннектор_КаналОтправка);
		
	НастройкиСИ = Новый НастройкиСервисаИнтеграции;
	НастройкиСИ.АдресВнешнегоСервисаИнтеграции = АдресВнешнегоСервисаИнтеграции;
	НастройкиСИ.ИмяПользователяВнешнегоСервисаИнтеграции = ИмяПользователяВнешнегоСервисаИнтеграции;
	НастройкиСИ.ПарольПользователяВнешнегоСервисаИнтеграции = ПарольПользователяВнешнегоСервисаИнтеграции;
	
	СервисыИнтеграции.Коннектор_1СШина.УстановитьНастройки(НастройкиСИ);
	СервисыИнтеграции.Коннектор_1СШина.УстановитьАктивность(АктивностьСервиса);
	
КонецПроцедуры

#КонецОбласти
