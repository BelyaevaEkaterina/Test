
// Заполняет перечень доступных к выгрузке объектов метаданных в соответствии с компонентами обмена.
Процедура ЗаполнениеПравилВыгрузки() Экспорт
	
	Если ВариантВыгрузки = Перечисления.ВариантыВыгрузки.EnterpriseData Тогда
		КомпонентыОбмена = Коннектор_БиблиотекаФункцийПовтИсп.КомпонентыОбмена("Отправка", ВерсияФормата, РасширениеФормата);
	КонецЕсли;
	
	ТаблицаПравилВыгрузки.Строки.Очистить();
	УзелДереваСправочники = ТаблицаПравилВыгрузки.Строки.Добавить();
	УзелДереваСправочники.ЭтоГруппа = Истина;
	УзелДереваСправочники.Наименование = НСтр("ru = 'Справочники'"); 
	УзелДереваСправочники.ПолноеИмяМетаданных = НСтр("ru = 'Справочники'");  
	
	УзелДереваДокументы = ТаблицаПравилВыгрузки.Строки.Добавить();
	УзелДереваДокументы.ЭтоГруппа = Истина;
	УзелДереваДокументы.Наименование = НСтр("ru = 'Документы'");
	УзелДереваДокументы.ОтборПоПериоду = Истина; 
	УзелДереваДокументы.ПолноеИмяМетаданных = НСтр("ru = 'Документы'");

	УзелДереваПВХ = ТаблицаПравилВыгрузки.Строки.Добавить();
	УзелДереваПВХ.ЭтоГруппа = Истина;
	УзелДереваПВХ.Наименование = НСтр("ru = 'Планы видов характеристик'");
	УзелДереваПВХ.ПолноеИмяМетаданных = НСтр("ru = 'Планы видов характеристик'");
	
	Если ВариантВыгрузки = Перечисления.ВариантыВыгрузки.EnterpriseData Тогда
		Для Каждого СтрокаПОД Из КомпонентыОбмена.ПравилаОбработкиДанных Цикл
			
			ТекМД = СтрокаПОД.ОбъектВыборкиМетаданные;
			Если ТекМД = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ТекИмя = ТекМД.Имя;
			ТекСиноним = ТекМД.Синоним;
			ПолноеИмяМДСтрокой = "";
			НовСтрока = Неопределено;
			Если Метаданные.Справочники.Содержит(ТекМД) Тогда
				НовСтрока = УзелДереваСправочники.Строки.Добавить();
				ПолноеИмяМДСтрокой = "Справочник." + ТекИмя;
				Представление = НСтр("ru = 'Справочник %1'");
			ИначеЕсли Метаданные.Документы.Содержит(ТекМД) Тогда
				НовСтрока = УзелДереваДокументы.Строки.Добавить();
				НовСтрока.ОтборПоПериоду = Истина;
				ПолноеИмяМДСтрокой = "Документ." + ТекИмя;
				Представление = НСтр("ru = 'Документ %1'");
			ИначеЕсли  Метаданные.ПланыВидовХарактеристик.Содержит(ТекМД) Тогда
				НовСтрока = УзелДереваПВХ.Строки.Добавить();
				ПолноеИмяМДСтрокой = "ПланВидовХарактеристик." + ТекИмя;
				Представление = НСтр("ru = 'План видов характеристик %1'");
			Иначе
				// Выгрузка других объектов метаданных не поддерживается.
				Продолжить;
			КонецЕсли;
			Представление = СтрЗаменить(Представление, "%1", ТекСиноним);
			НовСтрока.ЭтоГруппа = Ложь;
			НовСтрока.Наименование = ТекСиноним;
			НовСтрока.ПолноеИмяМетаданных = ПолноеИмяМДСтрокой;
			НовСтрока.Представление = Представление;
			
		КонецЦикла;
	КонецЕсли;    
	
	Если ВариантВыгрузки <> Перечисления.ВариантыВыгрузки.EnterpriseData Тогда
		
		Для Каждого Стр Из Метаданные.Справочники Цикл
			НовСтрока = УзелДереваСправочники.Строки.Добавить();
			НовСтрока.ПолноеИмяМетаданных = Стр.ПолноеИмя();
			НовСтрока.Представление = Стр.Представление();
		КонецЦикла;
		
		Для Каждого Стр Из Метаданные.Документы Цикл
			НовСтрока = УзелДереваДокументы.Строки.Добавить();
			НовСтрока.ПолноеИмяМетаданных = Стр.ПолноеИмя();
			НовСтрока.Представление = Стр.Представление();	
		КонецЦикла;
		
		Для Каждого Стр Из Метаданные.ПланыВидовХарактеристик Цикл
			НовСтрока = УзелДереваПВХ.Строки.Добавить();
			НовСтрока.ПолноеИмяМетаданных = Стр.ПолноеИмя();
			НовСтрока.Представление = Стр.Представление();	
		КонецЦикла;
		
	КонецЕсли;
	
	// Удалим лишние ветки
	Если УзелДереваСправочники.Строки.Количество() = 0 Тогда
		ТаблицаПравилВыгрузки.Строки.Удалить(УзелДереваСправочники);
	КонецЕсли; 
	
	Если УзелДереваДокументы.Строки.Количество() = 0 Тогда
		ТаблицаПравилВыгрузки.Строки.Удалить(УзелДереваДокументы);
	КонецЕсли;    
	
	Если УзелДереваПВХ.Строки.Количество() = 0 Тогда
		ТаблицаПравилВыгрузки.Строки.Удалить(УзелДереваПВХ);
	КонецЕсли;
	
КонецПроцедуры