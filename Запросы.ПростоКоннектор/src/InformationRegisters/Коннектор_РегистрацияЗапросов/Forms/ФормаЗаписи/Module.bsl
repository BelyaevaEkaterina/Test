
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отправить(Команда)
	ОтправкаСообщенияНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// <Описание процедуры>
//
// Параметры:
//  НастройкаБазы   - СправочникСсылка.ПравилаВыгрузкиДаннах - Настройка, содержащая параметры для отправки сообщения.
//  ТелоСообщения   - Строка                                - Текст сообщения для отправки.
//
&НаСервере
Процедура ОтправкаСообщенияНаСервере()
	
	Попытка
		
		// Проверка активности сервера
		АктивностьСервиса = СервисыИнтеграции.Коннектор_1СШина.ПолучитьАктивность();
		Если Не АктивностьСервиса Тогда
			Коннектор_БиблиотекаФункций.ЛогированиеПроцесса("ОтправкаСообщения",
				УровеньЖурналаРегистрации.Ошибка, ,
				"Сервис интеграции Коннектор_1СШина неактивен.");
		    Возврат;
		КонецЕсли;		
		
		// Создание сообщения
		СообщениеОбмена = СервисыИнтеграции.Коннектор_1СШина.СоздатьСообщение();
		СообщениеОбмена.КодПолучателя = Запись.КодПолучателя;
		ТелоКакПоток = СообщениеОбмена.ПолучитьТелоКакПоток();
		
		// Работа с телом сообщения
		Буфер = ПолучитьБуферДвоичныхДанныхИзСтроки(Запись.ТелоСообщения, "utf-8");
		ТелоКакПоток.Записать(Буфер, 0, Буфер.Размер);
    	ТелоКакПоток.Закрыть();
		
		// Установка параметров сообщения и отправка
		СообщениеОбмена.Параметры.Вставить("РазмерСообщения", Буфер.Размер);
		СообщениеОбмена.Параметры.Вставить("КодПравила", Запись.КодПравила);
		СервисыИнтеграции.Коннектор_1СШина[Запись.Канал].ОтправитьСообщение(СообщениеОбмена);
		
		// ++ Логирование ЛарченкоА
		РегистрацияОтправленногоСообщения(СообщениеОбмена, Запись.ТелоСообщения);
		// -- Логирование ЛарченкоА  

	Исключение   
		
		ТекстСообщения = ОписаниеОшибки() + 
		". Не удалось отправить сообщение:" +
		Запись.Идентификатор;
		
		Коннектор_БиблиотекаФункций.ЛогированиеПроцесса("ОтправкаСообщения",
			УровеньЖурналаРегистрации.Ошибка, ,
			ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры 

&НаСервере
Процедура РегистрацияОтправленногоСообщения(Знач СообщениеОбмена, Знач ТелоСообщения)
	МенеджерЗаписи = РегистрыСведений.Коннектор_РегистрацияЗапросов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = СообщениеОбмена.ДатаОтправки;
	МенеджерЗаписи.Канал = Запись.Канал;
	МенеджерЗаписи.Направление = Перечисления.Направление.Выгрузка;
	МенеджерЗаписи.Источник = Запись.Источник;
	МенеджерЗаписи.Идентификатор = СообщениеОбмена.Идентификатор;
	МенеджерЗаписи.ДатаУстаревания = СообщениеОбмена.ДатаУстаревания;
	МенеджерЗаписи.Отправлено = Истина;
	МенеджерЗаписи.КодПравила = Запись.КодПравила; 
	МенеджерЗаписи.ТелоСообщения = ТелоСообщения; 
	МенеджерЗаписи.Записать(); 
	
	// Запись в журнал 
	ТекстСообщения = "Формирование сообщений шины." + Символы.ПС + СообщениеОбмена.Идентификатор 
	+ Символы.ПС + Запись.ТелоСообщения;
	
	ЗаписьЖурналаРегистрации("ОтправкаСообщения",
	УровеньЖурналаРегистрации.Информация,
	Метаданные.РегистрыСведений.Коннектор_РегистрацияЗапросов, ,
	ТекстСообщения);
КонецПроцедуры	

#КонецОбласти

