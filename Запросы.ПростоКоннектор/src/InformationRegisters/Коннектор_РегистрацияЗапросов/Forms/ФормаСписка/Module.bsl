#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура Отправить(Команда)
	Результат = Коннектор_БиблиотекаФункций.СтруктураИзПараметровФормы(Элементы.Список.ТекущиеДанные);
	ОтправитьСообщениеНаСервере(Результат);
КонецПроцедуры

#КонецОбласти                       

#Область СлужебныеПроцедурыИФункции 
// <Описание процедуры>
//
// Параметры:
//  НастройкаБазы   - СправочникСсылка.ПравилаВыгрузкиДаннах - Настройка, содержащая параметры для отправки сообщения.
//  ТелоСообщения   - Строка                                - Текст сообщения для отправки.
//
&НаСервереБезКонтекста
Процедура ОтправитьСообщениеНаСервере(ПараметрыСообщения)
	
	Попытка
		// Проверка активности сервера
		АктивностьСервиса = СервисыИнтеграции.Коннектор_1СШина.ПолучитьАктивность();
		Если Не АктивностьСервиса Тогда
			Коннектор_БиблиотекаФункций.ЛогированиеПроцесса("ОтправкаСообщения",
				УровеньЖурналаРегистрации.Ошибка, ,
				"Сервис интеграции Коннектор_1СШина неактивен.");
		    Возврат;
		КонецЕсли;		
		
		// Получение информации о правиле выгрузки
		// Запись информации о начале обработки    
		ТекстСообщения = "Формирование сообщений шины." + Символы.ПС + ПараметрыСообщения.ТелоСообщения;
		Коннектор_БиблиотекаФункций.ЛогированиеПроцесса("ОтправкаСообщения", , , ТекстСообщения);
		
		// Создание сообщения
		СообщениеОбмена = СервисыИнтеграции.Коннектор_1СШина.СоздатьСообщение();
		СообщениеОбмена.КодПолучателя = ПараметрыСообщения.КодПолучателя;
		ТелоКакПоток = СообщениеОбмена.ПолучитьТелоКакПоток();
		
		// Работа с телом сообщения
		Буфер = ПолучитьБуферДвоичныхДанныхИзСтроки(ПараметрыСообщения.ТелоСообщения, "utf-8");
		ТелоКакПоток.Записать(Буфер, 0, Буфер.Размер);
    	ТелоКакПоток.Закрыть();
		
		// Установка параметров сообщения и отправка
		СообщениеОбмена.Параметры.Вставить("РазмерСообщения", Буфер.Размер);
		СообщениеОбмена.Параметры.Вставить("КодПравила", ПараметрыСообщения.КодПравила);
		СервисыИнтеграции.Коннектор_1СШина[ПараметрыСообщения.Канал].ОтправитьСообщение(СообщениеОбмена);
		
		// ++ Логирование ЛарченкоА
		РегистрацияОтправленногоСообщения(СообщениеОбмена, ПараметрыСообщения);
		// -- Логирование ЛарченкоА
	Исключение   
		
		ТекстСообщения = ОписаниеОшибки() + 
		". Не удалось отправить сообщение:" +
		ПараметрыСообщения.Идентификатор;
		
		Коннектор_БиблиотекаФункций.ЛогированиеПроцесса("ОтправкаСообщения",
			УровеньЖурналаРегистрации.Ошибка, ,
			ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура РегистрацияОтправленногоСообщения(Знач СообщениеОбмена, Знач ПараметрыСообщения)
	МенеджерЗаписи = РегистрыСведений.Коннектор_РегистрацияЗапросов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = СообщениеОбмена.ДатаОтправки;
	МенеджерЗаписи.Канал = ПараметрыСообщения.Канал;
	МенеджерЗаписи.Направление = Перечисления.Направление.Выгрузка;
	МенеджерЗаписи.Источник = ПараметрыСообщения.Источник;
	МенеджерЗаписи.Идентификатор = СообщениеОбмена.Идентификатор;
	МенеджерЗаписи.ДатаУстаревания = СообщениеОбмена.ДатаУстаревания;
	МенеджерЗаписи.Отправлено = Истина;
	МенеджерЗаписи.КодПравила = ПараметрыСообщения.КодПравила; 
	МенеджерЗаписи.ТелоСообщения = ПараметрыСообщения.ТелоСообщения; 
	МенеджерЗаписи.Записать();  
	
	// Запись в журнал 
	ТекстСообщения = "Формирование сообщений шины." + Символы.ПС + СообщениеОбмена.Идентификатор 
	+ Символы.ПС + ПараметрыСообщения.ТелоСообщения;
	
	ЗаписьЖурналаРегистрации("ОтправкаСообщения",
	УровеньЖурналаРегистрации.Информация,
	Метаданные.РегистрыСведений.Коннектор_РегистрацияЗапросов, ,
	ТекстСообщения);
КонецПроцедуры	
#КонецОбласти