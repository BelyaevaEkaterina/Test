
#Область СлужебныйПрограммныйИнтерфейс

// <Описание функции>
// Получает настройки выгрузки для заданного объекта метаданных.
//
// Параметры:
//  ИмяМетаданных  - Тип: Строка, полное имя метаданных, для которого нужно определить настройки выгрузки.
//          
// Возвращаемое значение:
//   Массив - Список структур с настройками выгрузки, каждая структура содержит следующие поля: см НастройкаВыгрузкиПоУмолчанию()
//
Функция ПравилаВыгрузки(Знач ИмяМетаданных) Экспорт
	
	Ответ = Новый Массив;
	
	Запрос = Новый Запрос;  
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Спр.Ссылка КАК ПравилоСсылка,
		|	Спр.УсловияВыгрузкиИсполныемыйКод КАК УсловияВыгрузкиИсполныемыйКод,
		|	ВЫБОР
		|		КОГДА Спр.ВариантПроверкиУсловияВыгрузки = ЗНАЧЕНИЕ(Перечисление.ВариантыПроверкиУсловийВыгрузки.СКД)
		|				И Спр.УсловиеВыгрузкиСКДУстановлено = ИСТИНА
		|			ТОГДА Спр.НастройкиСКД
		|		КОГДА Спр.ВариантВыгрузки = ЗНАЧЕНИЕ(Перечисление.ВариантыВыгрузки.СКД)
		|			ТОГДА Спр.НастройкиСКД
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НастройкиСКД,
		|	Спр.ИсполняемыйКод КАК ИсполняемыйКод,
		|	Спр.ВариантПроверкиУсловияВыгрузки КАК ВариантПроверкиУсловияВыгрузки,
		|	Спр.КодПолучателя КАК КодПолучателя,
		|	Спр.Код КАК КодПравила,
		|	Спр.УсловиеВыгрузкиСКДУстановлено КАК УсловиеВыгрузкиСКДУстановлено,
		|	Спр.Источник КАК Источник,
		|	Спр.ВариантВыгрузки КАК ВариантВыгрузки,
		|	Спр.ВерсияФормата КАК ВерсияФормата,
		|	Спр.РасширениеФормата КАК РасширениеФормата,
		|	ВЫБОР
		|		КОГДА Спр.ВариантВыгрузки = ЗНАЧЕНИЕ(Перечисление.ВариантыВыгрузки.КД2)
		|			ТОГДА Спр.ПравилоКонвертации.ХранилищеПравила
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ПравилоКонвертации,
		|	Коннектор_КаналОтправка.Значение КАК КаналОтправка
		|ИЗ
		|	Справочник.ПравилаВыгрузкиДаннах КАК Спр,
		|	Константа.Коннектор_КаналОтправка КАК Коннектор_КаналОтправка
		|ГДЕ
		|	Спр.Активно
		|	И Спр.Источник = &Источник
		|	И Спр.ПометкаУдаления = ЛОЖЬ
		|	И НЕ Коннектор_КаналОтправка.Значение ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Источник", ИмяМетаданных);
	Результат = Запрос.Выполнить(); 
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();  
		Пока Выборка.Следующий() Цикл
			
			Структура = НастройкаВыгрузкиПоУмолчанию();
			ЗаполнитьЗначенияСвойств(Структура, Выборка);
			
			Ответ.Добавить(Структура);	 
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

// <Описание функции>
// Возвращает стурктуру по умолчанию настроек правил выгрузки.
//
Функция НастройкаВыгрузкиПоУмолчанию() Экспорт

	Возврат Новый Структура("УсловияВыгрузкиИсполныемыйКод,
			|НастройкиСКД, ИсполняемыйКод, ВариантПроверкиУсловияВыгрузки,
			|КодПолучателя, КодПравила, УсловиеВыгрузкиСКДУстановлено, Источник, 
			|ВариантВыгрузки, ВерсияФормата, РасширениеФормата, ПравилоКонвертации, 
			|КаналОтправка, ПравилоСсылка");
	
КонецФункции

// <Описание функции>
// Получает настройки выгрузки для заданного объекта метаданных на основе активных подключаемых баз.
//
// Параметры:
//  Источник  - Тип: СправочникСсылка или ДокументСсылка - Объект метаданных, для которого нужно определить настройки выгрузки.
//              Должен быть объектом типа справочника или документа.
//
// Возвращаемое значение:
//   Массив - Список структур с настройками выгрузки, каждая структура содержит следующие поля:
//            * ИмяБазы           - Строка - Представление подключаемой базы данных.
//            * Префикс           - Строка - Префикс базы данных для выгрузки.
//            * Канал             - Строка - Канал интеграции.
//            * Сервис            - Строка - Сервис интеграции.
//            * ПравилоВыгрузки   - СправочникСсылка.ПравилаВыгрузкиДаннах - Правила выгрузки для объекта метаданных.
//   Ложь - Если нет активных подключаемых баз или правила выгрузки не найдены.
//
Функция ПолучитьНастройкиВыгрузкиБаз(Знач ИмяМетаданных) Экспорт
	
	// Выбор активных баз
	АктивныеБазы = ПолучитьАктивныеБазы();
	Если АктивныеБазы.Количество() = 0 Тогда
		Возврат Ложь;
	Конецесли; 
	
    // Формируем запрос для получения настроек выгрузки
	ПодключаемыеБазы = Новый Массив;
    Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", АктивныеБазы);
	Запрос.УстановитьПараметр("Метаданные", ИмяМетаданных);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПодключаемыеБазыНастройкиВыгрузкиДанных.Ссылка.Представление КАК База,
	               |	ПодключаемыеБазыНастройкиВыгрузкиДанных.Ссылка.Канал КАК Канал,
	               |	ПодключаемыеБазыНастройкиВыгрузкиДанных.Ссылка.СервисИнтеграции КАК СервисИнтеграции,
	               |	ПодключаемыеБазыНастройкиВыгрузкиДанных.Ссылка.КодПолучателя КАК КодПолучателя,
	               |	ПравилаВыгрузкиДаннах.Наименование КАК ПравилоВыгрузки,
	               |	ПравилаВыгрузкиДаннах.ИсполняемыйКод КАК ИсполняемыйКод
	               |ИЗ
	               |	Справочник.ПодключаемыеБазы.НастройкиВыгрузкиДанных КАК ПодключаемыеБазыНастройкиВыгрузкиДанных
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаВыгрузкиДаннах КАК ПравилаВыгрузкиДаннах
	               |		ПО ПодключаемыеБазыНастройкиВыгрузкиДанных.ПравилоВыгрузки = ПравилаВыгрузкиДаннах.Ссылка
	               |ГДЕ
	               |	ПодключаемыеБазыНастройкиВыгрузкиДанных.Ссылка В(&Ссылка)
	               |	И ПодключаемыеБазыНастройкиВыгрузкиДанных.Метаданные = &Метаданные
	               |	И ПодключаемыеБазыНастройкиВыгрузкиДанных.Выбрать
	               |	И ПравилаВыгрузкиДаннах.Ссылка <> ЗНАЧЕНИЕ(Справочник.ПравилаВыгрузкиДаннах.ПустаяСсылка)
	               |	И НЕ ПравилаВыгрузкиДаннах.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл  
		// Формируем структуру для базы
		СтруктураПодключения = Новый Структура;
		СтруктураПодключения.Вставить("ИмяБазы", Выборка.База);
		СтруктураПодключения.Вставить("КодПолучателя", Выборка.КодПолучателя);
		СтруктураПодключения.Вставить("Канал", Выборка.Канал);
		СтруктураПодключения.Вставить("Сервис", Выборка.СервисИнтеграции);
		СтруктураПодключения.Вставить("ПравилоВыгрузки", Выборка.ПравилоВыгрузки);
		СтруктураПодключения.Вставить("ИсполняемыйКод", Выборка.ИсполняемыйКод);
		ПодключаемыеБазы.Добавить(СтруктураПодключения);
	КонецЦикла;	
	
	Если ПодключаемыеБазы.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ПодключаемыеБазы;
КонецФункции

// <Описание функции>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
Функция ПолучитьНастройкиЗагрузкиБаз(Знач ИмяМетаданных, ИсполняемыйКод) Экспорт
	
	// Выбор активных баз
	АктивныеБазы = ПолучитьАктивныеБазы();
	Если АктивныеБазы.Количество() = 0 Тогда
		Возврат Ложь;
	Конецесли; 
	
    // Формируем запрос для получения настроек загрузки
    Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", АктивныеБазы);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПравилаЗагрузкиДанных.ИсполняемыйКод КАК ИсполняемыйКод,
	               |	ПравилаЗагрузкиДанных.Представление КАК Представление
	               |ИЗ
	               |	Справочник.ПодключаемыеБазы.НастройкиЗагрузки КАК ПодключаемыеБазыНастройкиЗагрузки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаЗагрузкиДанных КАК ПравилаЗагрузкиДанных
	               |		ПО ПодключаемыеБазыНастройкиЗагрузки.ПравилоЗагрузки = ПравилаЗагрузкиДанных.Ссылка
	               |			И ПодключаемыеБазыНастройкиЗагрузки.Ссылка.ФорматВыгрузкиЗагрузки = ПравилаЗагрузкиДанных.ФорматЗагрузки
	               |ГДЕ
	               |	ПодключаемыеБазыНастройкиЗагрузки.Ссылка.Ссылка В(&Ссылка)
	               |	И НЕ ПодключаемыеБазыНастройкиЗагрузки.Ссылка.ПометкаУдаления
	               |	И ПодключаемыеБазыНастройкиЗагрузки.ТипМетаданных = &ТипМетаданных
	               |	И НЕ ПравилаЗагрузкиДанных.ПометкаУдаления
	               |	И ПодключаемыеБазыНастройкиЗагрузки.Ссылка.Направление = ЗНАЧЕНИЕ(Перечисление.Направление.Загрузка)";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИсполняемыйКод = Выборка.ИсполняемыйКод;
	КонецЦикла;
	
КонецФункции

// <Описание функции>
// Получает версию БСП.
//
// Возвращаемое значение:
//   Строка версией БСП, либо пустая строка, если не удалось определить версию.
//
Функция ВерсияБСП() Экспорт
	
	Версия = "";
	Если Метаданные.ОбщиеМодули.Найти("СтандартныеПодсистемыСервер") <> Неопределено Тогда
		Возврат СтандартныеПодсистемыСервер.ВерсияБиблиотеки();
    КонецЕсли;      
	
КонецФункции
	
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// <Описание функции>
// Возвращает список ссылок на активные подключаемые базы данных.
//
// Параметры:
//   Нет
//
// Возвращаемое значение:
//   Массив - Массив ссылок на активные подключаемые базы данных.
//            Если активных баз нет, возвращается пустой массив.
//
Функция ПолучитьАктивныеБазы()
	ЗапросАктивныхБаз = Новый Запрос;
	ЗапросАктивныхБаз.Текст = "ВЫБРАТЬ
	                          |	ПодключаемыеБазы.Ссылка КАК Ссылка
	                          |ИЗ
	                          |	Справочник.ПодключаемыеБазы КАК ПодключаемыеБазы
	                          |ГДЕ
	                          |	ПодключаемыеБазы.Активна
	                          |	И НЕ ПодключаемыеБазы.ПометкаУдаления";
	
	Возврат ЗапросАктивныхБаз.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
КонецФункции 

Функция КомпонентыОбмена(Знач НаправлениеОбмена,
		Знач ВерсияФормата = "",
		Знач РасширениеФормата= "",
		Знач ВерсияФорматаОбменаПриЗагрузке = "",
		Знач РасширениеФорматаПриЗагрузке = "") Экспорт 
	
	КомпонентыОбмена     = ОбменДаннымиXDTOСервер.ИнициализироватьКомпонентыОбмена(НаправлениеОбмена);
	ТекВерсияФормата     = ?(НаправлениеОбмена = "Отправка", ВерсияФормата, ВерсияФорматаОбменаПриЗагрузке);
	ТекРасширениеФормата = ?(НаправлениеОбмена = "Отправка", РасширениеФормата, РасширениеФорматаПриЗагрузке);
	КомпонентыОбмена.ЭтоОбменЧерезПланОбмена = Ложь;
	КомпонентыОбмена.КлючСообщенияЖурналаРегистрации = "Выгрузка в формате EnterpriseData";
	КомпонентыОбмена.ВерсияФорматаОбмена = ТекВерсияФормата;
	КомпонентыОбмена.XMLСхема = "http://v8.1c.ru/edi/edi_stnd/EnterpriseData/" + ТекВерсияФормата;
	ОбменДаннымиXDTOСервер.ВключитьПространствоИмен(КомпонентыОбмена, ТекРасширениеФормата, "ext");
	
	ВерсииФорматаОбмена = Новый Соответствие;
	ОбменДаннымиПереопределяемый.ПриПолученииДоступныхВерсийФормата(ВерсииФорматаОбмена);
	КомпонентыОбмена.МенеджерОбмена = ВерсииФорматаОбмена.Получить(ТекВерсияФормата);
	Если КомпонентыОбмена.МенеджерОбмена = Неопределено Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не поддерживается версия формата обмена: <%1>.'"), ТекВерсияФормата);
	КонецЕсли;
	
	ОбменДаннымиXDTOСервер.ИнициализироватьТаблицыПравилОбмена(КомпонентыОбмена);
	ОбменДаннымиXDTOСервер.ПослеИнициализацииКомпонентыОбмена(КомпонентыОбмена);
	
	Возврат КомпонентыОбмена;
	
КонецФункции  

Функция ПравилаЗагрузкиДанных(КодПравила) Экспорт

	Возврат Справочники.ПравилаЗагрузкиДанных.ПравилаЗагрузки(КодПравила);
	
КонецФункции

#КонецОбласти